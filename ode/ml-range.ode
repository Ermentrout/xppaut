# The Morris-Lecar equations ml1.ode
#  NOTE: I use only the first letter for declarations!
# Declare the parameters
p gl=.5,gca=1,gk=2
p vk=-.7,vl=-.5,vca=1
p v1=.01,v2=.145,v3=.1,v4=.15
p i=.2,phi=.333   
  
w nz
# Define some functions
minf(v)=.5*(1+tanh((v-v1)/v2))
winf(v)= .5*(1+tanh((v-v3)/v4))
lamw(v)= cosh((v-v3)/(2*v4))

# define the right-hand sides
v'= gl*(vl-v)+gk*w*(vk-v)-icaf+i+sig*nz
w'= phi*lamw(v)*(winf(v)-w)

# where
icaf=gca*minf(v)*(v-vca)

# and
aux ica=icaf
par sig=0
# some initial conditions -- not necessary but for completeness
v(0)=.05
w(0)=0
@ total=100,trans=50
@ xp=v,yp=w,xlo=-.4,xhi=.6,ylo=-.1,yhi=.6
@ range=1,rangeover=i,rangestep=50,rangelow=-.2,rangehigh=.5
@ rangereset=yes
@ dfgrid=16,ncdraw=1,dfdraw=2
# Done!!
d